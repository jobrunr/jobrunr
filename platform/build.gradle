plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'signing'
}

version = System.getenv('CI_COMMIT_TAG') ?: "v1.0.0-SNAPSHOT"

dependencies {
    constraints {
        api 'org.slf4j:slf4j-api:2.0.17'
        api 'org.ow2.asm:asm:9.8'
        api 'org.ow2.asm:asm-util:9.8'

        api 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
        api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0'
        api 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.20.0'
        api 'com.fasterxml.jackson.module:jackson-module-kotlin:2.19.1'
        api 'com.google.code.gson:gson:2.13.2'
        api 'org.eclipse:yasson:3.0.4'
        api 'io.micrometer:micrometer-core:1.15.1'
        api 'io.insert-koin:koin-core:4.1.0'
        api 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0'

        api 'org.xerial:sqlite-jdbc:3.50.1.0'
        api 'com.h2database:h2:2.3.232'
        api 'org.postgresql:postgresql:42.7.7'
        api 'com.google.cloud.sql:postgres-socket-factory:1.25.1'
        api 'com.oracle.database.jdbc:ojdbc8:23.8.0.25.04'
        api 'org.mariadb.jdbc:mariadb-java-client:3.5.3'
        api 'com.mysql:mysql-connector-j:9.3.0'
        api 'com.microsoft.sqlserver:mssql-jdbc:12.10.1.jre8'
        api 'com.ibm.db2:jcc:12.1.2.0'
        api 'com.zaxxer:HikariCP:6.3.0'
        api 'io.agroal:agroal-pool:2.7.1'
        api 'org.apache.commons:commons-dbcp2:2.13.0'
        api 'org.apache.tomcat:tomcat-jdbc:11.0.8'
        api 'org.mongodb:mongodb-driver-sync:5.5.1'

        api 'org.junit.jupiter:junit-jupiter:5.13.2'
        api 'io.github.artsok:rerunner-jupiter:2.1.6'
        api 'org.awaitility:awaitility:4.3.0'
        api 'org.assertj:assertj-core:3.27.3'
        api 'com.tngtech.archunit:archunit-junit5:1.4.1'
        api 'net.javacrumbs.json-unit:json-unit-assertj:2.40.1'
        api 'org.mockito:mockito-inline:5.2.0'
        api 'org.mockito:mockito-junit-jupiter:5.19.0'
        api 'ch.qos.logback:logback-classic:1.5.18'
        api 'org.testcontainers:testcontainers:1.21.3'
        api 'org.testcontainers:junit-jupiter:1.21.3'
        api 'org.testcontainers:jdbc:1.21.3'
        api 'org.testcontainers:mariadb:1.21.3'
        api 'org.testcontainers:mssqlserver:1.21.3'
        api 'org.testcontainers:mysql:1.21.3'
        api 'org.testcontainers:oracle-free:1.21.3'
        api 'org.testcontainers:postgresql:1.21.3'
        api 'org.testcontainers:db2:1.21.3'
        api 'com.microsoft.playwright:playwright:1.52.0'
        api 'org.wiremock:wiremock:3.13.1'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'jobrunr-bom'
            version = project.version
            from components.javaPlatform

            pom {
                name = 'JobRunr BOM'
                description = 'An easy way to perform background processing on the JVM. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/jobrunr/jobrunr'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 or later'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#lgpl-v3-license'
                    }
                    license {
                        name = 'Commercial License'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#commercial-license'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jobrunr/jobrunr.git'
                    developerConnection = 'scm:git@github.com:jobrunr/jobrunr.git'
                    url = 'https://github.com/jobrunr/jobrunr.git'
                }
            }
        }
    }
}

signing {
    String base64Key = System.getenv('SIGNING_KEY')
    if (base64Key) {
        useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
        sign publishing.publications.mavenJava
    }
}
