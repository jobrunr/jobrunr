import groovy.json.JsonSlurper

plugins {
    id 'org.sonarqube' version '4.4.1.3373'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'signing'
}

nexusPublishing {
    packageGroup = "org.jobrunr"
    repositories {
        // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

task nexusPushFromStagingToCentral {
    doLast {
        if (System.getenv("SONATYPE_AUTH_HEADER") == null) {
            throw new GradleException("Cannot push Sonatype Nexus artifacts to Portal API: env var SONATYPE_AUTH_HEADER is not set")
        }

        println("Retrieving uploaded OSSRH Staging API repository key...")
        def get = new URL("https://ossrh-staging-api.central.sonatype.com/manual/search/repositories?profile_id=org.jobrunr&state=open&ip=any").openConnection();
        get.setRequestProperty("Authorization", System.getenv("SONATYPE_AUTH_HEADER"))
        def resp = get.getInputStream().getText();
        def json = new JsonSlurper().parseText(resp)

        if (!get.getResponseCode().equals(200)) {
            throw new GradleException("Cannot find repository key from Sonatype OSSRH Staging API: received code " + get.getResponseCode())
        }
        if (json.repositories == null || json.repositories[0] == null) {
            throw new GradleException("Cannot find repository key from Sonatype OSSRH Staging API: no key returned, is the publication done? received json " + json)
        }
        def repoKey = json.repositories[0].key
        println("-- Found Repo Key " + repoKey)

        println("Uploading from OSSRH Staging API to Central Portal using retrieved key...")
        def post = new URL("https://ossrh-staging-api.central.sonatype.com/manual/upload/repository/" + repoKey).openConnection();
        post.setRequestProperty("Authorization", System.getenv("SONATYPE_AUTH_HEADER"))
        post.setRequestMethod("POST")
        post.setDoOutput(true)

        if (!post.getResponseCode().equals(200)) {
            throw new GradleException("Cannot push artifacts from staging API to Portal API: received code " + post.getResponseCode() + " - please log into https://central.sonatype.com/ to investigate what went wrong.")
        }
        println("-- Successfully pushed Sonatype Nexus artifacts to Portal API: please log into https://central.sonatype.com/ to finish the release.")
    }
}

subprojects {
    group = 'org.jobrunr'
    version = System.getenv('CI_COMMIT_TAG')?.replace("v", "") ?: "v1.0.0-SNAPSHOT".replace("v", "")
}

def isNonStable = { String version ->
    def isPreview = ['SNAPSHOT', 'ALPHA'].any { it -> version.toUpperCase().contains(it) }
    def isRelease = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /(?!\.)(\d+(\.\d+)+)(?![\d\.])?(\.jre\d)?/
    def isVersionMatch = (version ==~ regex)
    def result = isPreview || !(isRelease || isVersionMatch)
    return result
}

sonar {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", "jobrunr_jobrunr"
        property "sonar.organization", "jobrunr"
        property "sonar.branch.name", "v8"
        property "sonar.branch.target", "v8"
        property "sonar.coverage.jacoco.xmlReportPaths", "/tmp/reports/$project.name/jacocoTestCoverage.xml"
    }
}

configure(subprojects.findAll { !['platform'].contains(it.name) }) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = "UTF-8"
        options.release = 8
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        options.encoding = "UTF-8"
    }

    javadoc {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        implementation platform(project(':platform'))
        implementation 'org.apache.groovy:groovy-json:4.0.27'

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'io.github.artsok:rerunner-jupiter'
        testImplementation 'org.awaitility:awaitility'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'com.tngtech.archunit:archunit-junit5'
        testImplementation 'net.javacrumbs.json-unit:json-unit-assertj'
        testImplementation 'ch.qos.logback:logback-classic'
        testImplementation 'org.mockito:mockito-inline'
        testImplementation 'org.mockito:mockito-junit-jupiter'

        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.clean.doFirst {
        delete "/tmp/reports/$project.name"
        println "Deleted /tmp/reports/$project.name"
    }

    test {
        finalizedBy jacocoTestReport
        reports.junitXml.destination = file("/tmp/reports/$project.name")
        reports.html.destination = file("/tmp/reports/$project.name")

        useJUnitPlatform()
        testLogging {
            minGranularity = 3
            events "passed", "skipped", "failed"
        }
    }

    sonar {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.projectKey", "jobrunr_jobrunr"
            property "sonar.organization", "jobrunr"
            property "sonar.branch.name", System.getenv("CI_COMMIT_BRANCH") ?: "master"
            property "sonar.coverage.jacoco.xmlReportPaths", "/tmp/reports/$project.name/jacocoTestCoverage.xml"
        }
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    jacocoTestReport {
        reports {
            xml.required = true
            xml.destination = file("/tmp/reports/$project.name/jacocoTestCoverage.xml")
            csv.required = false
            html.required = false
        }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}