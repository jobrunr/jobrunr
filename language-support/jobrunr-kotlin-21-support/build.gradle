import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm' version '2.1.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.0'
}

dependencies {
    api project(':core')
    compileOnly 'io.insert-koin:koin-core'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-serialization-json'
    compileOnly 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation project(':core')
    testImplementation testFixtures(project(":core"))

    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    testImplementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-serialization-json'
}

dependencyUpdates {
    rejectVersionIf {
        it.candidate.group.startsWith('org.jetbrains.kotlin') && !it.candidate.version.startsWith('2.1')
    }
}

compileJava {
    options.release = 8
}

kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_1_8
    }
}

tasks.named('compileTestKotlin', KotlinCompile) {
    compilerOptions.jvmTarget.set(JvmTarget.JVM_21)
}

java {
    withJavadocJar()
    withSourcesJar()
}

normalization {
    runtimeClasspath {
        ignore '**/MANIFEST.MF'
        ignore '**/frontend/**'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'jobrunr-kotlin-2.1-support'
            version = project.version
            from components.java

            pom {
                name = 'JobRunr Kotlin 2.1 Support'
                description = 'An easy way to perform background processing in Kotlin. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/jobrunr/jobrunr'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 or later'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#lgpl-v3-license'
                    }
                    license {
                        name = 'Commercial License'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#commercial-license'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jobrunr/jobrunr.git'
                    developerConnection = 'scm:git@github.com:jobrunr/jobrunr.git'
                    url = 'https://github.com/jobrunr/jobrunr.git'
                }

                versionMapping {
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                withXml {
                    def pomNode = asNode()
                    pomNode.remove(pomNode.dependencyManagement)
                    pomNode.dependencies.'*'.each() {
                        if (it.optional.text() == 'true') {
                            it.parent().remove(it)
                        }
                    }
                }
            }
        }
    }
}

signing {
    String base64Key = System.getenv('SIGNING_KEY')
    if (base64Key) {
        useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
        sign publishing.publications.mavenJava
    }
}
