import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.release = 17
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    api platform(project(':platform'))
    api platform('org.springframework.boot:spring-boot-dependencies:3.3.4')
    annotationProcessor platform('org.springframework.boot:spring-boot-dependencies:3.3.4')

    api project(':core')
    api 'org.springframework.boot:spring-boot-starter'
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compileOnly 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'io.micrometer:micrometer-core'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind'
    compileOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compileOnly 'com.google.code.gson:gson'
    compileOnly 'org.eclipse:yasson'

    compileOnly 'org.springframework.data:spring-data-redis'
    compileOnly 'redis.clients:jedis'
    compileOnly 'io.lettuce:lettuce-core'
    compileOnly 'org.mongodb:mongodb-driver-sync'
    compileOnly 'co.elastic.clients:elasticsearch-java'

    testImplementation testFixtures(project(":core"))
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.google.code.gson:gson'
    testImplementation 'org.springframework.data:spring-data-redis'
    testImplementation 'redis.clients:jedis'
    testImplementation 'io.lettuce:lettuce-core'
    testImplementation 'org.mongodb:mongodb-driver-sync'
    testImplementation 'co.elastic.clients:elasticsearch-java'
    testImplementation 'io.micrometer:micrometer-core'
}

java {
    withJavadocJar()
    withSourcesJar()
}

normalization {
    runtimeClasspath {
        ignore '**/MANIFEST.MF'
        ignore '**/frontend/**'
    }
}

jar {
    archiveClassifier = ''
    manifest {
        attributes 'Automatic-Module-Name': 'org.jobrunr.spring.boot-3.starter'
        attributes 'Built-By': 'build.jobrunr.io'
        attributes 'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        attributes 'Build-Version': project.version
        attributes 'Implementation-Version': project.version
        attributes 'Bundle-Version': project.version
        attributes 'Created-By': "Gradle ${gradle.gradleVersion}"
        attributes 'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
        attributes 'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'jobrunr-spring-boot-3-starter'
            version = project.version
            from components.java

            pom {
                name = 'JobRunr Spring Boot Native'
                description = 'An easy way to perform background processing on the JVM. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/jobrunr/jobrunr'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 or later'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#lgpl-v3-license'
                    }
                    license {
                        name = 'Commercial License'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#commercial-license'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jobrunr/jobrunr.git'
                    developerConnection = 'scm:git@github.com:jobrunr/jobrunr.git'
                    url = 'https://github.com/jobrunr/jobrunr.git'
                }

                versionMapping {
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                withXml {
                    def pomNode = asNode()
                    pomNode.remove(pomNode.dependencyManagement)
                    pomNode.dependencies.'*'.each() {
                        if (it.optional.text() == 'true') {
                            it.parent().remove(it)
                        } else if (it.artifactId.text() == 'spring-boot-starter') {
                            it.scope.replaceNode {
                                scope('provided')
                            }
                        }
                    }
                }
            }
        }
    }
}

signing {
    String base64Key = System.getenv('SIGNING_KEY')
    if (base64Key) {
        useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
        sign publishing.publications.mavenJava
    }
}

sonar {
    skipProject = true
}