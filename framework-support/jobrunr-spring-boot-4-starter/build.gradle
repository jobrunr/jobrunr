import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

compileJava {
    options.release = 17
}

compileTestJava {
    options.release = 17
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {
    api project(':core')
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compileOnly 'org.springframework.boot:spring-boot-starter-classic'
    compileOnly 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'io.micrometer:micrometer-core'
    compileOnly 'com.fasterxml.jackson.core:jackson-annotations'
    compileOnly 'org.jetbrains.kotlinx:kotlinx-serialization-json'
    compileOnly project(':language-support:jobrunr-kotlin-22-support')

    compileOnly 'org.mongodb:mongodb-driver-sync'

    testImplementation testFixtures(project(":core"))
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'tools.jackson.core:jackson-databind'
    testImplementation 'com.google.code.gson:gson'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-serialization-json'
    testImplementation project(':language-support:jobrunr-kotlin-22-support')
    testImplementation 'org.springframework.data:spring-data-redis'
    testImplementation 'org.mongodb:mongodb-driver-sync'
    testImplementation 'io.micrometer:micrometer-core'
}

java {
    withJavadocJar()
    withSourcesJar()
}

normalization {
    runtimeClasspath {
        ignore '**/MANIFEST.MF'
        ignore '**/frontend/**'
    }
}

jar {
    archiveClassifier = ''
    manifest {
        attributes 'Automatic-Module-Name': 'org.jobrunr.spring.boot-4.starter'
        attributes 'Built-By': 'build.jobrunr.io'
        attributes 'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        attributes 'Build-Version': project.version
        attributes 'Implementation-Version': project.version
        attributes 'Bundle-Version': project.version
        attributes 'Created-By': "Gradle ${gradle.gradleVersion}"
        attributes 'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
        attributes 'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'jobrunr-spring-boot-4-starter'
            version = project.version
            from components.java

            pom {
                name = 'JobRunr Spring Boot 4 Starter'
                description = 'An easy way to perform background processing on the JVM. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/jobrunr/jobrunr'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 or later'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#lgpl-v3-license'
                    }
                    license {
                        name = 'Commercial License'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md#commercial-license'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jobrunr/jobrunr.git'
                    developerConnection = 'scm:git@github.com:jobrunr/jobrunr.git'
                    url = 'https://github.com/jobrunr/jobrunr.git'
                }

                versionMapping {
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }

                withXml {
                    def pomNode = asNode()
                    pomNode.remove(pomNode.dependencyManagement)
                    pomNode.dependencies.'*'.each() {
                        if (it.optional.text() == 'true') {
                            it.parent().remove(it)
                        }
                    }
                }
            }
        }
    }
}

signing {
    String base64Key = System.getenv('SIGNING_KEY')
    if (base64Key) {
        useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
        sign publishing.publications.mavenJava
    }
}

sonar {
    skipProject = true
}