import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.version.replace("v", "")
    ]
}

java {
    registerFeature('metrics') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    api platform(project(':platform'))
    api platform('io.quarkus:quarkus-bom:2.6.1.Final')
    annotationProcessor 'io.quarkus:quarkus-extension-processor:2.6.1.Final'

    api project(':core')
    implementation 'io.quarkus:quarkus-core'
    implementation 'io.quarkus:quarkus-arc'
    compileOnly 'io.quarkus:quarkus-micrometer'

    metricsApi 'io.quarkus:quarkus-smallrye-health'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind'
    compileOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compileOnly 'org.eclipse:yasson'

    compileOnly 'org.mongodb:mongodb-driver-sync'
    compileOnly 'org.elasticsearch.client:elasticsearch-rest-high-level-client'

    testImplementation testFixtures(project(":core"))
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'org.eclipse:yasson'
    testImplementation 'org.mongodb:mongodb-driver-sync'
    testImplementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.jobrunr'
            artifactId = 'quarkus-jobrunr'
            version = project.version.replace("v", "")
            from components.java

            pom {
                name = 'JobRunr Quarkus Extension'
                description = 'An easy way to perform background processing on the JVM. Backed by persistent storage. Open and free for commercial use.'
                url = 'https://github.com/jobrunr/jobrunr'
                licenses {
                    license {
                        name = 'Multi licensed'
                        url = 'https://github.com/jobrunr/jobrunr/blob/master/License.md'
                    }
                }
                developers {
                    developer {
                        id = 'rdehuyss'
                        name = 'Ronald Dehuysser'
                        email = 'ronald.dehuysser@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jobrunr/jobrunr.git'
                    developerConnection = 'scm:git@github.com:jobrunr/jobrunr.git'
                    url = 'https://github.com/jobrunr/jobrunr.git'
                }

                versionMapping {
                    usage('java-runtime'){
                        fromResolutionResult()
                    }
                }

                withXml {
                    def pomNode = asNode()
                    pomNode.remove(pomNode.dependencyManagement)
//                    pomNode.dependencies.'*'.findAll() {
//                        it.optional.text() == 'true'
//                    }.each() {
//                        it.parent().remove(it)
//                    }
                }
            }
        }
    }
}

signing {
    String base64Key = System.getenv('SIGNING_KEY')
    if (base64Key) {
        useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
        sign publishing.publications.mavenJava
    }
}